# Maintainer: sukanka <su975853527@gmail.com>
pkgname=kmre-display-control
pkgrel=1
pkgver=1.4.6
pkgdesc='Ubuntu Kylin KMRE, an android container.'
arch=('x86_64')
license=('GPL-2.0-only')
url='https://gitee.com/openkylin/kylin-kmre-display-control'
groups=(kmre)
source=(git+${url}.git)
sha512sums=('SKIP')
makedepends=(
    cmake
    git lsb-release
    ninja
    python
)
prepare() {
    cd ${srcdir}/kylin-${pkgname}
    sed -i host/display-control/display_control.c \
        -e '30a #include "utils/debug.h"'

}
build() {
    local cmake_args=(
        -G Ninja
        -DCMAKE_INSTALL_PREFIX=/usr
        -DCMAKE_INSTALL_LIBDIR=/usr/lib
        -DCMAKE_BUILD_TYPE=None
    )
    cd ${srcdir}/
    install -d build
    cmake -S kylin-${pkgname} -B build "${cmake_args[@]}"
    ninja -C build
}

package() {
    cd ${srcdir}/build
    DESTDIR="$pkgdir" ninja install
    cd ${srcdir}/kylin-${pkgname}/host
    cp -rf include ${pkgdir}/usr
}
