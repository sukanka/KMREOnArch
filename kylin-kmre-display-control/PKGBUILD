# Maintainer: sukanka <su975853527@gmail.com>
pkgname=kylin-kmre-display-control
pkgrel=1
pkgver=3.9.6
pkgdesc='display control libraries for kmre'
arch=('x86_64')
groups=(kylin-kmre)
license=('GPL-2.0-only')
url='https://gitee.com/openkylin/kylin-kmre-display-control'
makedepends=(cmake git lsb-release ninja python)
source=(git+${url}.git)
sha512sums=('SKIP')

pkgver() {
    cat ${srcdir}/${pkgname}/debian/control | grep Standards-Version | cut -d : -f2 | tr -d ' '
}

prepare() {
    cd ${srcdir}/${pkgname}
    sed -i host/display-control/display_control.c \
        -e '30a #include "utils/debug.h"'
}

build() {
    local cmake_args=(
        -G Ninja
        -DCMAKE_INSTALL_PREFIX=/usr
        -DCMAKE_INSTALL_LIBDIR=/usr/lib
        -DCMAKE_BUILD_TYPE=None
    )
    cd ${srcdir}/
    install -d build
    cmake -S ${pkgname} -B build "${cmake_args[@]}"
    ninja -C build
}

package() {
    cd ${srcdir}/build
    DESTDIR="$pkgdir" ninja install
    cd ${srcdir}/${pkgname}/host/include
    install -Dm644 {lib,}DisplayControl/*.h -t ${pkgdir}/usr/include/DisplayControl

}
