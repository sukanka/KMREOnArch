# Maintainer: sukanka <su975853527@gmail.com>
pkgname=kmre-daemon
pkgver=1.7.1
pkgrel=1
groups=(kmre)
pkgdesc='Ubuntu Kylin KMRE, an android container.'
arch=('x86_64')
license=('GPL-3.0-only')
url='https://gitee.com/openkylin/kylin-kmre-daemon'
depends=(
    dbus-c++
    fuse{2,3}
    glibmm
    jsoncpp
    openssl
)
makedepends=(
    cmake
    git lsb-release
    ninja

)
source=(git+https://gitee.com/openkylin/kylin-kmre-daemon.git)
sha512sums=('SKIP')

prepare() {
    cd ${srcdir}/kylin-${pkgname}
    sed -i CMakeLists.txt \
        -e 's|g++-9|g++|' \
        -e 's| /lib| /usr/lib|g'
    sed -i 's|<jsoncpp/json/json.h>|<json/json.h>|g' src/container/image-info.h
    sed -i '30a #include <cstdint> ' src/utils.h

}
build() {
    local cmake_args=(
        -G Ninja
        -DCMAKE_INSTALL_PREFIX=/usr
        -DCMAKE_INSTALL_SYSCONFDIR=/etc
        -DCMAKE_INSTALL_LIBDIR=/usr/lib
        -DCMAKE_BUILD_TYPE=None
        -DCMAKE_CXX_COMPILER=g++
    )
    cd ${srcdir}/
    install -d build
    cmake -S kylin-${pkgname} -B build "${cmake_args[@]}"
    ninja -C build
}

package() {
    cd ${srcdir}/build
    DESTDIR="$pkgdir" ninja install
}
