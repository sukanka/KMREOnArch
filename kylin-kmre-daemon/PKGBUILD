# Maintainer: sukanka <su975853527@gmail.com>
pkgname=kylin-kmre-daemon
pkgver=3.9.6
pkgrel=1
groups=(kylin-kmre)
pkgdesc='A DBus daemon for kmre'
arch=('x86_64')
license=('GPL-3.0-only')
url='https://gitee.com/openkylin/kylin-kmre-daemon'
depends=(
    dbus-c++
    fuse{2,3}
    glibmm
    jsoncpp
    openssl
)
makedepends=(cmake git lsb-release ninja)
source=(git+${url}.git)
sha512sums=('SKIP')

pkgver() {
    cat ${srcdir}/${pkgname}/debian/control | grep Standards-Version | cut -d : -f2 | tr -d ' '
}

prepare() {
    cd ${srcdir}/${pkgname}
    sed -i CMakeLists.txt \
        -e 's|g++-9|g++|' \
        -e 's| /lib| /usr/lib|g'
    sed -i 's|<jsoncpp/json/json.h>|<json/json.h>|g' src/container/image-info.h
    sed -i '30a #include <cstdint> ' src/utils.h

}

build() {
    local cmake_args=(
        -G Ninja
        -DCMAKE_INSTALL_PREFIX=/usr
        -DCMAKE_INSTALL_SYSCONFDIR=/etc
        -DCMAKE_INSTALL_LIBDIR=/usr/lib
        -DCMAKE_BUILD_TYPE=None
        -DCMAKE_CXX_COMPILER=g++
    )
    cd ${srcdir}/
    install -d build
    cmake -S ${pkgname} -B build "${cmake_args[@]}"
    ninja -C build
}

package() {
    cd ${srcdir}/build
    DESTDIR="$pkgdir" ninja install
}
