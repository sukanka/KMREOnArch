# Maintainer: sukanka <su975853527@gmail.com>
pkgname=kylin-kmre-emugl
pkgver=3.9.6
pkgrel=1
url='https://gitee.com/openkylin/kylin-kmre-emugl'
pkgdesc='emugl libraries for kylin-kmre'
arch=('x86_64')
license=('GPL-3.0-only')
groups=(kylin-kmre)
source=(git+${url}.git)
sha256sums=('SKIP')
makedepends=(git cmake ninja libunwind boost)
depends=(libx11 libglvnd libpciaccess libdrm)

pkgver() {
    cat ${srcdir}/${pkgname}/debian/control | grep Standards-Version | cut -d : -f2 | tr -d ' '
}

prepare() {
    cd ${pkgname}
    sed -i CMakeLists.txt \
        -e '13a cmake_policy(SET CMP0072 NEW)'
    sed -i android-emu/android/utils/gles_config_detection.cpp \
        -e '30a #include <cstdint>'
    sed -i android-emu/android/HostHwInfo.h \
        -e '17a #include <cstdint>'
    sed -i android-emugl/angle/src/common/angleutils.h \
        -e '20a #include <cstdint>'
    sed -i android-emugl/angle/include/GLSLANG/ShaderVars.h \
        -e '16a #include <cstdint>'
}

build() {
    local cmake_args=(
        -G Ninja
        -DCMAKE_INSTALL_PREFIX=/usr
        -DCMAKE_INSTALL_LIBDIR=/usr/lib
        -DCMAKE_BUILD_TYPE=None
    )
    install -d build
    cmake -S ${pkgname} -B build "${cmake_args[@]}"
    ninja -C build
}

package() {
    cd ${srcdir}/build
    DESTDIR="$pkgdir" ninja install
}
